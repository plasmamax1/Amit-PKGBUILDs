# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Amit Prayaga <amitprayaga@gmail.com>

pkgbase=mesa-amd-git
pkgname=('vulkan-mesa-layers-amd-git' 'vulkan-radeon-amd-git' 'libva-mesa-driver-amd-git' 'mesa-amd-git')
pkgdesc="An open-source implementation of the OpenGL specification"
pkgver=23.2.0_devel.170592.8c1bc1b964a
pkgrel=1
arch=('x86_64')
makedepends=('python-mako' 'libxml2' 'libx11' 'xorgproto' 'libdrm' 'libxshmfence' 'libxxf86vm'
             'libxdamage' 'libva' 'wayland' 'wayland-protocols' 'zstd' 'elfutils' 'llvm'
             'libclc' 'clang' 'libglvnd' 'libunwind' 'lm_sensors' 'libxrandr' 'systemd'
             'glslang' 'vulkan-icd-loader' 'cmake' 'meson')
url="https://www.mesa3d.org/"
license=('custom')
source=(git+https://gitlab.freedesktop.org/mesa/mesa.git)
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/mesa
  read -r _ver <VERSION
  echo ${_ver/-/_}.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  # Build only minimal debug info to reduce size
  CFLAGS+=' -g1'
  CXXFLAGS+=' -g1'

  # https://gitlab.freedesktop.org/mesa/mesa/-/issues/6229
  CFLAGS+=' -mtls-dialect=gnu'
  CXXFLAGS+=' -mtls-dialect=gnu'

  meson mesa build \
    -D b_ndebug=true \
    -D b_pie=true \
    -D b_lto=true
    -D buildtype=plain \
    -D wrap-mode=nodownload \
    -D platforms=x11,wayland \
    -D egl-native-platform=x11,wayland \
    -D android-libbacktrace=disabled \
    -D dri3=enabled \
    -D expat=enabled \
    -D gallium-drivers=radeonsi,zink \
    -D gallium-vdpau=disabled \
    -D gallium-omx=disabled \
    -D gallium-va=enabled \
    -D gallium-va=disabled \
    -D vulkan-drivers=amd \
    -D freedreno-kmds=[] \
    -D shader-cache=enabled \
    -D vulkan-layers=device-select,overlay \
    -D shared-glapi=enabled \
    -D gles1=disabled \
    -D gles2=enabled \
    -D gbm=enabled \
    -D glx=dri \
    -D egl=enabled \
    -D glvnd=enabled \
    -D microsoft-clc=disabled \
    -D llvm=disabled \
    -D valgrind=disabled \
    -D libunwind=enabled \
    -D lmsensors=enabled \
    -D enable-glcpp-tests=false \
    -D power8=disabled \
    -D xlib-lease=enabled \
    -D allow-kcmp=enabled \
    -D zstd=enabled \
    -D intel-clc=disabled \
    -D video-codecs=all \
    -D gallium-d3d12-video=disabled \
    -D xmlconfig=enabled \

  # Print config
  meson configure --no-pager build
  ninja -C build
  meson compile -C build

  # fake installation to be seperated into packages
  # outside of fakeroot but mesa doesn't need to chown/mod
  DESTDIR="${srcdir}/fakeinstall" meson install -C build
}

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}

package_vulkan-mesa-layers-amd-git() {
  pkgdesc="Mesa's Vulkan layers"
  depends=('libdrm' 'libxcb' 'wayland' 'python')
  provides=('vulkan-mesa-layers' 'vulkan-mesa-layer')
  conflicts=('vulkan-mesa-layers' 'vulkan-mesa-layer')
  

  _install fakeinstall/usr/share/vulkan/explicit_layer.d
  _install fakeinstall/usr/share/vulkan/implicit_layer.d
  _install fakeinstall/usr/lib/libVkLayer_*.so
  _install fakeinstall/usr/bin/mesa-overlay-control.py
}

package_vulkan-radeon-amd-git() {
  pkgdesc="Radeon's Vulkan mesa driver"
  depends=('wayland' 'libx11' 'libxshmfence' 'libelf' 'libdrm' 'llvm-libs' 'systemd-libs')
  optdepends=('vulkan-mesa-layers: additional vulkan layers')
  provides=('vulkan-radeon' 'vulkan-driver')
  conflicts=('vulkan-radeon' 'vulkan-driver')

  _install fakeinstall/usr/share/drirc.d/00-radv-defaults.conf
  _install fakeinstall/usr/share/vulkan/icd.d/radeon_icd*.json
  _install fakeinstall/usr/lib/libvulkan_radeon.so
}

package_libva-mesa-driver-amd-git() {
  pkgdesc="VA-API implementation for gallium"
  depends=('libdrm' 'libx11' 'llvm-libs' 'expat' 'libelf' 'libxshmfence')
  depends+=('libexpat.so')
  provides=('libva-mesa-driver')
  conflicts=('libva-mesa-driver')

  _install fakeinstall/usr/lib/dri/*_drv_video.so
}

package_mesa-amd-git() {
  depends=('libdrm' 'wayland' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf'
           'libomxil-bellagio' 'libunwind' 'llvm-libs' 'lm_sensors' 'libglvnd'
           'zstd' 'vulkan-icd-loader')
  depends+=('libsensors.so' 'libexpat.so' 'libvulkan.so')
  optdepends=('opengl-man-pages: for the OpenGL API man pages'
              'mesa-vdpau: for accelerated video playback'
              'libva-mesa-driver: for accelerated video playback')
  provides=('mesa' 'mesa-libgl' 'opengl-driver')
  conflicts=('mesa' 'mesa-libgl' 'opengl-driver')

  _install fakeinstall/usr/share/drirc.d/00-mesa-defaults.conf
  _install fakeinstall/usr/share/glvnd/egl_vendor.d/50_mesa.json

  # ati-dri, nouveau-dri, intel-dri, svga-dri, swrast, swr
  _install fakeinstall/usr/lib/dri/*_dri.so

  #_install fakeinstall/usr/lib/bellagio
  #_install fakeinstall/usr/lib/d3d
  _install fakeinstall/usr/lib/lib{gbm,glapi}.so*
  #_install fakeinstall/usr/lib/libOSMesa.so*
  #_install fakeinstall/usr/lib/libxatracker.so*

  _install fakeinstall/usr/include
  _install fakeinstall/usr/lib/pkgconfig

  # libglvnd support
  _install fakeinstall/usr/lib/libGLX_mesa.so*
  _install fakeinstall/usr/lib/libEGL_mesa.so*

  # indirect rendering
  ln -s /usr/lib/libGLX_mesa.so.0 "${pkgdir}/usr/lib/libGLX_indirect.so.0"

  # make sure there are no files left to install
  find fakeinstall -depth -print0 | xargs -0 rmdir
}
